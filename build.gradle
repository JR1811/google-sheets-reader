plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
	id 'java'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

configurations.all {
	resolutionStrategy {
		force("net.fabricmc:fabric-loader:$loader_version")
	}
}

repositories {
	// Google Sheets API
	mavenCentral()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Google Sheets API
	implementation 'com.google.api-client:google-api-client:2.6.0'
	implementation 'com.google.oauth-client:google-oauth-client-jetty:1.36.0'
	implementation 'com.google.apis:google-api-services-sheets:v4-rev20240826-2.0.0'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			from components.java
			artifactId = project.archives_base_name
			version = project.mod_version
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	// Specifically for Repsy, and how to manage the credentials, check out https://docs.repsy.io/maven/using-repository-with-gradle/
	repositories {
		maven {
			name = 'repsy'
			url = uri("https://repo.repsy.io/mvn/fabricated-atelier/fabric-sheets-reader/")
			credentials {
				username = project.findProperty("repsyUsername") ?: ""
				password = project.findProperty("repsyPassword") ?: ""
			}
		}
	}
}